//import sekcija
import java_cup.runtime.*;
import java.io.*;

parser code {:

   public int errNo = 0;
   
   public static void main( String[] args )
   {
      try {
           FileReader file = new FileReader(args[0]);
           Scanner scanner = new MPLexer(file);
           MPParser parser = new MPParser(scanner);
           parser.parse();
           if (parser.errNo == 0)
              System.out.println("Analiza zavrsena. U kodu nema gresaka.");
           else
              System.out.println("Analiza zavrsena. Broj gresaka: " + parser.errNo);
      } catch(Exception e) {
           e.printStackTrace();
      }
   }
   
   public void syntax_error(Symbol cur_token) {}
   
   public void report_error(String message, Object info) {
       System.out.print(message);
   }

   public int getLine() {
      return ((MPLexer)getScanner()).getLine();
   }

:};

// Terminali
terminal PROGRAM, BEGIN, END;
terminal INTEGER, CHAR, REAL, BOOLEAN;
terminal SELECT, CASE;
terminal OR, AND;
terminal ID, CONST;

terminal ASSIGN, ARROW;
terminal LT, LE, EQ, NE, GT, GE;

terminal LEFTPAR, RIGHTPAR;
terminal SEMICOLON, COMMA, DOT, COLON;

// Neterminali
non terminal Program, Block, Variables, Declaration, NameList, Type;
non terminal StatementList, Statement, SelectStatement, CaseList, Case;
non terminal Expression, AndExpression, RelExpression, RelOp, Term;

// GRAMATIKA

Program ::= PROGRAM Block DOT
           {: System.out.println("Redukcija 1: Program ::= program Block ."); :}
         | PROGRAM Block error
           {: 
              System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje '.' na kraju programa");
              parser.errNo++;
           :}
         | PROGRAM error
           {: 
              System.out.println("Greska u liniji " + parser.getLine() + ": Telo programa je nekorektno");
              parser.errNo++;
           :}
         | error
           {: 
              System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje kljucna rec 'program'");
              parser.errNo++;
           :}
         ;

Block ::= BEGIN Variables StatementList END
        {: System.out.println("Redukcija 2: Block ::= begin Variables StatementList end"); :}
      | BEGIN Variables StatementList error
        {: 
           System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje 'end'");
           parser.errNo++;
        :}
      | BEGIN error
        {: 
           System.out.println("Greska u liniji " + parser.getLine() + ": Nekorektno telo bloka");
           parser.errNo++;
        :}
      ;

Variables ::= Variables Declaration
            {: System.out.println("Redukcija 3: Variables ::= Variables Declaration"); :}
          | /* epsilon */
            {: System.out.println("Redukcija 4: Variables ::= epsilon"); :}
          ;

Declaration ::= NameList COLON Type SEMICOLON
              {: System.out.println("Redukcija 5: Declaration ::= NameList : Type ;"); :}
            | NameList COLON Type error
              {: 
                 System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje ';' u deklaraciji");
                 parser.errNo++;
              :}
            | NameList COLON error
              {: 
                 System.out.println("Greska u liniji " + parser.getLine() + ": Nekorektan tip");
                 parser.errNo++;
              :}
            ;

NameList ::= NameList COMMA ID
           {: System.out.println("Redukcija 6: NameList ::= NameList , ID"); :}
         | NameList COMMA error
           {: 
              System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje identifikator nakon ','");
              parser.errNo++;
           :}
         | ID
           {: System.out.println("Redukcija 7: NameList ::= ID"); :}
         ;

Type ::= INTEGER
       {: System.out.println("Redukcija 8: Tip ::= integer"); :}
     | CHAR
       {: System.out.println("Redukcija 9: Tip ::= char"); :}
     | REAL
       {: System.out.println("Redukcija 10: Tip ::= real"); :}
     | BOOLEAN
       {: System.out.println("Redukcija 11: Tip ::= boolean"); :}
     ;

StatementList ::= StatementList Statement
                {: System.out.println("Redukcija 12: StatementList ::= StatementList Statement"); :}
              | Statement
                {: System.out.println("Redukcija 13: StatementList ::= Statement"); :}
              ;

Statement ::= SelectStatement
            {: System.out.println("Redukcija 14: Statement ::= SelectStatement"); :}
          | ID ASSIGN Expression SEMICOLON
            {: System.out.println("Redukcija 15: Statement ::= ID := Expression ;"); :}
          | ID ASSIGN Expression error
            {: 
               System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje ';' nakon dodele");
               parser.errNo++;
            :}
          | ID ASSIGN error
            {: 
               System.out.println("Greska u liniji " + parser.getLine() + ": Nekorektan izraz u dodeli");
               parser.errNo++;
            :}
          | Block
            {: System.out.println("Redukcija 16: Statement ::= Block"); :}
          ;

SelectStatement ::= SELECT BEGIN CaseList END
                  {: System.out.println("Redukcija 17: SelectStatement ::= select begin CaseList end"); :}
                | SELECT BEGIN CaseList error
                  {: 
                     System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje 'end' u select naredbi");
                     parser.errNo++;
                  :}
                | SELECT error
                  {: 
                     System.out.println("Greska u liniji " + parser.getLine() + ": Nekorektan select");
                     parser.errNo++;
                  :}
                ;

CaseList ::= CaseList Case
           {: System.out.println("Redukcija 18: CaseList ::= CaseList Case"); :}
         | Case
           {: System.out.println("Redukcija 19: CaseList ::= Case"); :}
         ;

Case ::= CASE Expression ARROW Statement
       {: System.out.println("Redukcija 20: Case ::= case Expression => Statement"); :}
     | CASE Expression ARROW error
       {: 
          System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje naredba u case grani");
          parser.errNo++;
       :}
     | CASE error
       {: 
          System.out.println("Greska u liniji " + parser.getLine() + ": Nekorektan case");
          parser.errNo++;
       :}
     ;

Expression ::= Expression OR AndExpression
             {: System.out.println("Redukcija 21: Expression ::= Expression or AndExpression"); :}
           | AndExpression
             {: System.out.println("Redukcija 22: Expression ::= AndExpression"); :}
           ;

AndExpression ::= AndExpression AND RelExpression
                {: System.out.println("Redukcija 23: AndExpression ::= AndExpression and RelExpression"); :}
              | RelExpression
                {: System.out.println("Redukcija 24: AndExpression ::= RelExpression"); :}
              ;

RelExpression ::= Term RelOp Term
                {: System.out.println("Redukcija 25: RelExpression ::= Term RelOp Term"); :}
              | Term
                {: System.out.println("Redukcija 26: RelExpression ::= Term"); :}
              ;

RelOp ::= LT {: System.out.println("Redukcija 27: RelOp ::= <"); :}
        | LE {: System.out.println("Redukcija 28: RelOp ::= <="); :}
        | EQ {: System.out.println("Redukcija 29: RelOp ::= =="); :}
        | NE {: System.out.println("Redukcija 30: RelOp ::= <>"); :}
        | GT {: System.out.println("Redukcija 31: RelOp ::= >"); :}
        | GE {: System.out.println("Redukcija 32: RelOp ::= >="); :}
        ;

Term ::= ID {: System.out.println("Redukcija 33: Term ::= ID"); :}
       | CONST {: System.out.println("Redukcija 34: Term ::= CONST"); :}
       | LEFTPAR Expression RIGHTPAR {: System.out.println("Redukcija 35: Term ::= ( Expression )"); :}
       | LEFTPAR Expression error
         {: 
            System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje ')'");
            parser.errNo++;
         :}
       | LEFTPAR error
         {: 
            System.out.println("Greska u liniji " + parser.getLine() + ": Nekorektan izraz u zagradama");
            parser.errNo++;
         :}
       ;
